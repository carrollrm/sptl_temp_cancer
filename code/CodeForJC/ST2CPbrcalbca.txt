model{
for (i in 1:33551){	#individuals with brca
	temp1[i]<-(log(timeF1[i]+0.01)-log(lam1[i]))/sigma1
	log(lam1[i])<-beta1[1]+beta1[2]*x11[i]+beta1[3]*x12[i]+beta1[4]*x13[i]+beta1[5]*x14[i]+
		beta1[6]*x15[i]+beta1[7]*x16[i]+beta1[8]*x17[i]+beta1[9]*x18[i]+
		beta1[10]*x19[i]+beta1[11]*x110[i]+beta1[12]*x111[i]+beta1[13]*x112[i]+
		beta1[14]*x113[i]+beta1[15]*x114[i]+beta1[16]*x115[i]+
		u[spt1[i]]+gamA1[yr1[i]]+gamB1[evnt1[i]]
#logistic distn surv and density
	s1[i]<-1/(1+exp(temp1[i]))
	f1[i]<-exp(temp1[i])*pow(s1[i],2)
#log like
	L1[i]<-indic1[i]*log(f1[i]/(sigma1*(timeF1[i]+0.01)))+(1-indic1[i])*log(s1[i])
#Poisson zeros trick
	zeros1[i]<-0
	new1[i]<- -L1[i]
	zeros1[i]~dpois(new1[i])
}#close i loop for brca
for (i in 1:45987){	#individuals with lbca
	temp2[i]<-(log(timeF2[i]+0.01)-log(lam2[i]))/sigma2
	log(lam2[i])<-beta2[1]+beta2[2]*x21[i]+beta2[3]*x22[i]+beta2[4]*x23[i]+beta2[5]*x24[i]+
		beta2[6]*x25[i]+beta2[7]*x26[i]+beta2[8]*x27[i]+beta2[9]*x28[i]+
		beta2[10]*x29[i]+beta2[11]*x210[i]+beta2[12]*x211[i]+beta2[13]*x212[i]+
		u[spt2[i]]+gamA2[yr2[i]]+gamB2[evnt2[i]]
#logistic distn surv and density
	s2[i]<-1/(1+exp(temp2[i]))
	f2[i]<-exp(temp2[i])*pow(s2[i],2)
#log like
	L2[i]<-indic2[i]*log(f2[i]/(sigma2*(timeF2[i]+0.01)))+(1-indic2[i])*log(s2[i])
#Poisson zeros trick
	zeros2[i]<-0
	new2[i]<- -L2[i]
	zeros2[i]~dpois(new2[i])
}#close i loop  for lbca
#model params for all
for (j in 1:16){beta1[j]~dnorm(0,0.001)}#brca
for (j in 1:13){beta2[j]~dnorm(0,0.001)}#lbca
tauu<-pow(sdu,-2)
sdu~dunif(0,10)
sigma2~dunif(0.01,10)
sigma1~dunif(0.01,10)
for (j in 1:64){u[j]~dnorm(0,tauu)}#close j loop

gamA1[1]<-1
taugamA1<-pow(sdgamA1,-2)
sdgamA1~dunif(0,10)
gamA2[1]<-1
taugamA2<-pow(sdgamA2,-2)
sdgamA2~dunif(0,10)
for (k in 2:14){
	gamA1[k]~dnorm(gamA1[k-1],taugamA1)
	gamA2[k]~dnorm(gamA2[k-1],taugamA2)
}#close k loop

gamB1[1]<-0
gamB1[2]~dnorm(0,taugamB1)
taugamB1<-pow(sdgamB1,-2)
sdgamB1~dunif(0,10)
gamB2[1]<-0
gamB2[2]~dnorm(0,taugamB2)
taugamB2<-pow(sdgamB2,-2)
sdgamB2~dunif(0,10)

#Change point
cpA1~dunif(a,b)
cpA11<-cpA1+9
cpA2~dunif(cpA11,c)
for (i in 1:33551){evnt1[i]<-step(tick1[i]-cpA1)-step(tick1[i]-cpA2)+1}

cpB1~dunif(a,b)
cpB11<-cpB1+9
cpB2~dunif(cpB11,c)
for (i in 1:45987){evnt2[i]<-step(tick2[i]-cpB1)-step(tick2[i]-cpB2)+1}



#deviances
D1<-(-2)*sum(L1[])
D2<-(-2)*sum(L2[])
}#close model loop